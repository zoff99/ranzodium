//file:noinspection SpellCheckingInspection
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.zoffcc.applications.ranzodium_example'
    //noinspection GradleDependency
    compileSdk 34

    def getGitHash = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--verify', '--short=8', 'HEAD'
                standardOutput = stdout
            }
            return "\"" + stdout.toString().trim() + "\""
        }
        catch(Exception ignored)
        {
            return "\"" + "????????" + "\""
        }
    }

    defaultConfig {
        applicationId 'com.zoffcc.applications.ranzodium_example'
        minSdk 21
        //noinspection OldTargetApi
        targetSdk 33
        versionCode 10000
        versionName "1.0.0"

        buildConfigField "String", "GIT_HASH", getGitHash()

        // Enabling multidex support
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            crunchPngs false
            ext.enableCrashlytics = false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            crunchPngs false
            ext.enableCrashlytics = false
            minifyEnabled false
            shrinkResources false
            // proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // we need desugaring here to support APIs down to 21
        coreLibraryDesugaringEnabled true
        // we need desugaring here to support APIs down to 21
        encoding "UTF-8"
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
    lintOptions {
        abortOnError false
    }
    buildFeatures {
        compose true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            exclude 'kotlin-tooling-metadata.json'
            exclude 'version-control-info.textproto'
            exclude '/META-INF/*.version'
            // https://github.com/Kotlin/kotlinx.coroutines/issues/2274
            exclude 'DebugProbesKt.bin'
        }
    }
}

dependencies {
    // we need desugaring here to support APIs down to 21
    //noinspection GradleDependency
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
    // we need desugaring here to support APIs down to 21

    implementation 'com.github.zoff99:pkgs_ranzodium-jni-lib:1.0.1'

    //noinspection GradleDependency
    implementation 'androidx.core:core-ktx:1.8.0'
    //noinspection GradleDependency
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    //noinspection GradleDependency
    implementation 'androidx.activity:activity-compose:1.5.1'
    //noinspection GradleDependency
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    // implementation 'androidx.compose.material3:material3-android:1.3.2'
    implementation 'androidx.compose.material3:material3:1.3.2'
    implementation 'androidx.compose.material:material-icons-core'
    implementation 'androidx.compose.material:material-icons-extended'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    //noinspection GradleDependency
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
